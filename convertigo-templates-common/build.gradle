org.apache.tools.ant.DirectoryScanner.removeDefaultExclude('**/.gitignore')
org.apache.tools.ant.DirectoryScanner.removeDefaultExclude('**/.gitattributes')

task copyTemplatesCommons {
	group 'convertigo'
}

rootProject.file('convertigo-templates').eachDir {
	if (!it.name.startsWith('.') && it.name != 'common') {
		def pdir = it
		copyTemplatesCommons.dependsOn task("copyCommonsFor${it.name.capitalize()}", type: Copy) {
			from('common') {
				exclude '**/*.jar'
				filter { it.replace('((CURRENT_VERSION))', version.replace('beta', 'SNAPSHOT')) }
			}
			
			from('common') {
				include '**/*.jar'
			}
			
			inputs.properties(
				'version': version
			)
			
			
			into pdir
		}
	}
}

task('buildTemplates', type: GradleBuild, dependsOn: [copyTemplatesCommons, ':gradle-plugin:publishToMavenLocal']) {
	group 'convertigo'
	
	tasks = ['buildTemplates']
	
	dir = rootProject.file('convertigo-templates')
	
	startParameter.projectProperties = [
		'convertigo.plugin.version': version.replace('beta', 'SNAPSHOT'),
		'convertigo.car.destinationDir': project(':eclipse-plugin-studio').file('tomcat/webapps/convertigo/templates/project'),
		'convertigo.load.projectVersion': version,
		'convertigo.performsMobileBuild': 'false'
	]
}