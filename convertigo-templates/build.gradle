if (!rootProject.name.equals(name)) {

	org.apache.tools.ant.DirectoryScanner.removeDefaultExclude('**/.gitignore')
	org.apache.tools.ant.DirectoryScanner.removeDefaultExclude('**/.gitattributes')

	task copyTemplatesCommons {
		group 'convertigo'
	}
	
	projectDir.eachDir {
		if (!it.name.startsWith('.') && it.name != 'common') {
			def pdir = it
			copyTemplatesCommons.dependsOn task("copyCommonsFor${it.name.capitalize()}", type: Copy) {
				from('common') {
					exclude '**/*.jar'
					filter { it.replace('((CURRENT_VERSION))', version.replace('beta', 'SNAPSHOT')) }
				}
				
				from('common') {
					include '**/*.jar'
				}
				
				inputs.properties(
					'version': version
				)
				
				
				into pdir
			}
		}
	}
	
	task('buildTemplates', type: GradleBuild, dependsOn: [copyTemplatesCommons, ':gradle-plugin:publishToMavenLocal']) {
		group 'convertigo'
		
		tasks = ['buildTemplates']
		
		startParameter.projectProperties = [
			'convertigo.plugin.version': version.replace('beta', 'SNAPSHOT'),
			'convertigo.car.destinationDir': project(':eclipse-plugin-studio').file('tomcat/webapps/convertigo/templates/project'),
			'convertigo.load.projectVersion': version,
			'convertigo.performsMobileBuild': 'false'
		]
	}
	
} else {

	buildscript {
		repositories {
			mavenLocal()
			maven {
				url "https://m2.convertigo.com"
			}
			jcenter()
		}
		dependencies {
			classpath "com.convertigo:gradle-plugin:${property 'convertigo.plugin.version'}"
		}
	}
	
	task buildTemplates {}
	
	subprojects {
		afterEvaluate {
			buildTemplates.dependsOn tasks['car']
		}
	}
	
}